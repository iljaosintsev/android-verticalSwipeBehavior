apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'maven-publish'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

bintray {
    def propFile = file("local.properties")
    if (!propFile.exists()) return
    def prop = new Properties()
    prop.load(new FileInputStream(propFile))

    user = prop.getProperty("bintrayUser")
    key = prop.getProperty("bintrayApiKey")
    group = project.property('group')
    version = project.property("nameVersion")
    publications = ["VerticalSwipeBehavior"]

    pkg {
        repo = project.property("repo")
        name = project.property('name')
        licenses = ["MIT"]
        vcsUrl = project.property('gitUrl')
        websiteUrl = project.property('siteUrl')
        publicDownloadNumbers = true
        version {
            name = project.property("nameVersion")
            released = new Date()
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
        }
    }
    developers {
        developer {
            name project.property('user')
            email project.property('email')
        }
    }
    scm {
        connection project.property('gitUrl')
        developerConnection project.property('gitUrl')
        url project.property('siteUrl')
    }
}

publishing {
    publications {
        VerticalSwipeBehavior(MavenPublication) {
            artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
            artifact sourcesJar
            pom.withXml {
                def root = asNode()
                root.appendNode("name", project.property('name'))
                root.appendNode("description", project.property('desc'))
                root.appendNode("url", "https://apiqa.io")
                root.children().last() + pomConfig
            }
        }
    }
}
